# SPDX-FileCopyrightText: 2026 Gundu Labs
# SPDX-License-Identifier: GPL-3.0-or-later

# Daemon, CLI, and PAM modules for Gaze (mirrors packaging/nfpm.yaml).
{
  lib,
  rustPlatform,
  pkg-config,
  makeWrapper,
  clang,
  glib,
  gst_all_1,
  onnxruntime,
  opencv,
  openssl,
  pipewire,
  tpm2-tss,
}:

let
  # Runtime plugins from the `out` output; gstreamer's default output is `bin`, which has none.
  gstPluginPath = lib.makeSearchPathOutput "lib" "lib/gstreamer-1.0" [
    gst_all_1.gstreamer
    gst_all_1.gst-plugins-base
    gst_all_1.gst-plugins-good
    pipewire
  ];
in
rustPlatform.buildRustPackage {
  pname = "gaze";
  version = (builtins.fromTOML (builtins.readFile ../../Cargo.toml)).workspace.package.version;

  src = lib.fileset.toSource {
    root = ../..;
    fileset = lib.fileset.unions [
      ../../Cargo.toml
      ../../Cargo.lock
      ../../README.md
      ../../crates
      ../../packaging/config
      ../../packaging/nix/nixos-module.nix
    ];
  };

  cargoLock.lockFile = ../../Cargo.lock;

  nativeBuildInputs = [
    pkg-config
    makeWrapper
    # The opencv crate generates its bindings with libclang at build time.
    rustPlatform.bindgenHook
    clang
  ];

  buildInputs = [
    glib
    gst_all_1.gstreamer
    gst_all_1.gst-plugins-base
    onnxruntime
    opencv
    openssl
    tpm2-tss
  ];

  # Two invocations keep gaze-vision's `detection` feature out of the clients.
  buildPhase = ''
    runHook preBuild
    cargo build --release --offline -p gazed
    cargo build --release --offline -p gaze-cli -p pam-gaze -p pam-gaze-grosshack
    runHook postBuild
  '';

  # The test suite expects a camera and a running system bus.
  doCheck = false;

  installPhase = ''
    runHook preInstall
    install -Dm755 target/release/gazed $out/bin/gazed
    install -Dm755 target/release/gaze $out/bin/gaze
    install -Dm755 target/release/libpam_gaze.so $out/lib/security/pam_gaze.so
    install -Dm755 target/release/libpam_gaze_grosshack.so $out/lib/security/pam_gaze_grosshack.so
    install -Dm644 packaging/config/config.toml $out/share/gaze/config.toml
    install -Dm644 packaging/config/com.gundulabs.Gaze.conf $out/share/dbus-1/system.d/com.gundulabs.Gaze.conf
    install -Dm644 packaging/config/com.gundulabs.gaze.policy $out/share/polkit-1/actions/com.gundulabs.gaze.policy
    runHook postInstall
  '';

  # `--set`, not `--prefix`: a second GStreamer build inherited from the session
  # registers the same plugin types again and the scanner rejects the duplicates.
  postFixup = ''
    wrapProgram $out/bin/gazed \
      --set GST_PLUGIN_SYSTEM_PATH_1_0 "${gstPluginPath}" \
      --set GAZE_CPU_ORT_PATH "${lib.getLib onnxruntime}/lib/libonnxruntime.so"
    wrapProgram $out/bin/gaze \
      --set GST_PLUGIN_SYSTEM_PATH_1_0 "${gstPluginPath}"
  '';

  meta = {
    description = "Daemon, CLI, and PAM integration for Gaze facial authentication";
    homepage = "https://gaze.gundulabs.com";
    license = lib.licenses.gpl3Plus;
    platforms = [
      "x86_64-linux"
      "aarch64-linux"
    ];
    mainProgram = "gaze";
  };
}
