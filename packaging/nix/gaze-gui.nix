# SPDX-FileCopyrightText: 2026 Gundu Labs
# SPDX-License-Identifier: GPL-3.0-or-later

# GTK4/Adwaita GUI for Gaze (mirrors packaging/nfpm-gui.yaml).
{
  lib,
  rustPlatform,
  pkg-config,
  wrapGAppsHook4,
  clang,
  glib,
  gst_all_1,
  gtk4,
  libadwaita,
  opencv,
  openssl,
  pipewire,
}:

let
  # `out`, not the default output; see packaging/nix/gaze.nix.
  gstPluginPath = lib.makeSearchPathOutput "lib" "lib/gstreamer-1.0" [
    gst_all_1.gstreamer
    gst_all_1.gst-plugins-base
    gst_all_1.gst-plugins-good
    pipewire
  ];
in
rustPlatform.buildRustPackage {
  pname = "gaze-gui";
  version = (builtins.fromTOML (builtins.readFile ../../Cargo.toml)).workspace.package.version;

  src = lib.fileset.toSource {
    root = ../..;
    fileset = lib.fileset.unions [
      ../../Cargo.toml
      ../../Cargo.lock
      ../../README.md
      ../../crates
      ../../packaging/gui
    ];
  };

  cargoLock.lockFile = ../../Cargo.lock;

  # Keeps gaze-vision's `detection` feature (ONNX Runtime) out of the GUI.
  cargoBuildFlags = [
    "--package"
    "gaze-gui"
  ];

  nativeBuildInputs = [
    pkg-config
    wrapGAppsHook4
    # The opencv crate generates its bindings with libclang at build time.
    rustPlatform.bindgenHook
    clang
  ];

  buildInputs = [
    glib
    gst_all_1.gstreamer
    gst_all_1.gst-plugins-base
    gtk4
    libadwaita
    opencv
    openssl
  ];

  # The test suite expects a camera and a running system bus.
  doCheck = false;

  postInstall = ''
    install -Dm644 packaging/gui/com.gundulabs.Gaze.desktop $out/share/applications/com.gundulabs.Gaze.desktop
    install -Dm644 packaging/gui/com.gundulabs.Gaze.svg $out/share/icons/hicolor/scalable/apps/com.gundulabs.Gaze.svg
    install -Dm644 packaging/gui/com.gundulabs.Gaze.metainfo.xml $out/share/metainfo/com.gundulabs.Gaze.metainfo.xml
  '';

  # `--set`, not `--prefix`; see packaging/nix/gaze.nix.
  preFixup = ''
    gappsWrapperArgs+=(--set GST_PLUGIN_SYSTEM_PATH_1_0 "${gstPluginPath}")
  '';

  meta = {
    description = "GTK4/Adwaita GUI for Gaze facial authentication";
    homepage = "https://gaze.gundulabs.com";
    license = lib.licenses.gpl3Plus;
    platforms = [
      "x86_64-linux"
      "aarch64-linux"
    ];
    mainProgram = "gaze-gui";
  };
}
